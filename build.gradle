plugins {
    id 'java'
    id 'maven-publish'
}

group 'com.fabbroniko'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/fabbroniko/universal-logger")
        credentials {
            username = project.findProperty("github.username") ?: System.getenv("GITHUB_USERNAME")
            password = project.findProperty("github.token.read") ?: System.getenv("GITHUB_READ_TOKEN")
        }
    }
}

sourceSets {
    integrationTest {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
}

tasks.register('integrationTest', Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn integrationTest

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.example.Main'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

integrationTest {
    useJUnitPlatform()
}

dependencies {
    implementation 'com.fabbroniko:universal-logger:1.1.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.11.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.11.0'
    testImplementation 'org.mockito:mockito-core:5.12.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.12.0'
    testImplementation 'org.assertj:assertj-core:3.26.3'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/fabbroniko/simple-dependency-injector")
            credentials {
                username = project.findProperty("github.username") ?: System.getenv("USERNAME")
                password = project.findProperty("github.token") ?: System.getenv("TOKEN")
            }
        }
    }

    publications {
        register("jar", MavenPublication) {
            from components.java
        }
    }
}

